architecture avr {

//---------------------------------------------------------------------------
//  ENCODING FORMATS OF INSTRUCTIONS
//---------------------------------------------------------------------------

    format GPRGPR = { opcode[5:0], r2[4:4], r1[4:4], r1[3:0], r2[3:0] };
    format GPR = { opcode[6:0], r1[4:4], r1[3:0], 0b0101 };
    format HGPRIMM8 = { opcode[3:0], imm1[7:4], r1[3:0], imm1[3:0] };
    format BRSET = { 0b111100, target[6:0], bit[2:0] };
    format BRCLR = { 0b111101, target[6:0], bit[2:0] };
    format SET = { 0b10010100, 0b0, bit[2:0], 0b1000 };
    format CLR = { 0b10010100, 0b1, bit[2:0], 0b1000 };
    format ABS4 = { opcode[9:3], target[21:17], opcode[2:0], target[16:0] };
    format MGPRMGPR = { opcode[7:0], s1[0], r1[2:0], s2[0], r2[2:0] };


//---------------------------------------------------------------------------
//  OPERANDS ALLOWED TO INSTRUCTIONS
//---------------------------------------------------------------------------

    operand GPR[5]: symbol = {
        r0  = 0,  r1  = 1,  r2  = 2,  r3  = 3,
        r4  = 4,  r5  = 5,  r6  = 6,  r7  = 7,
        r8  = 8,  r9  = 9,  r10 = 10, r11 = 11,
        r12 = 12, r13 = 13, r14 = 14, r15 = 15,
        r16 = 16, r17 = 17, r18 = 18, r19 = 19,
        r20 = 20, r21 = 21, r22 = 22, r23 = 23,
        r24 = 24, r25 = 25, r26 = 26, r27 = 27,
        r28 = 28, r29 = 29, r30 = 30, r31 = 31
    };

    operand HGPR[4]: symbol = {
        r16 = 0,  r17 = 1,  r18 = 2,  r19 = 3,
        r20 = 4,  r21 = 5,  r22 = 6,  r23 = 7,
        r24 = 8,  r25 = 9,  r26 = 10, r27 = 11,
        r28 = 12, r29 = 13, r30 = 14, r31 = 15
    };

    operand MGPR[3]: symbol = {
        r16 = 0, r17 = 1, r18 = 2, r19 = 3,
        r20 = 4, r21 = 5, r22 = 6, r23 = 7
    };

    operand YZ[1]: symbol = {
        Y = 1, Z = 0
    };

    operand Z[0]: symbol = {
        Z = 0
    };

    operand EGPR[4]: symbol = {
        r0  = 0,  r2  = 1,  r4  = 2,  r6  = 3,
        r8  = 4,  r10 = 5,  r12 = 6,  r14 = 7,
        r16 = 8,  r18 = 9,  r20 = 10, r22 = 11,
        r24 = 12, r26 = 13, r28 = 14, r30 = 15
    };

    operand RDL[2]: symbol = {
        r24 = 0, r26 = 1, r28 = 2, r30 = 3
    };

    operand XYZ[2]: symbol = {
        X = 0, Y = 1, Z = 2
    };

    operand IMM3[3]: immediate = [0, 7];
    operand IMM5[5]: immediate = [0, 31];
    operand IMM6[6]: immediate = [0, 63];
    operand IMM7[7]: immediate = [0, 127];
    operand IMM8[8]: immediate = [0, 255];
    operand SREL[7]: relative = [-64, 63];
    operand LREL[11]: relative = [-1024, 1023];
    operand PADDR[16]: word = [0, 65536]; // this is encoded as a word address
    operand DADDR[16]: address = [0, 65536];

//---------------------------------------------------------------------------
//  SUBROUTINES
//---------------------------------------------------------------------------
    inline subroutine performAddition(r1: int, r2: int, carry: int): byte {
        local result: int = r1 + r2 + carry;
        local ral: int = r1[3:0];
        local rbl: int = r2[3:0];

        local Rd7: boolean = r1[7];
        local Rr7: boolean = r2[7];
        local R7: boolean = result[7];

        // set the flags as per instruction set documentation.
        H = (ral + rbl + carry)[4];
        C = result[8];
        N = result[7];
        Z = low(result) == 0;
        V = (Rd7 and Rr7 and !R7) or (!Rd7 and !Rr7 and R7);
        S = N xor V;

        return low(result);
    };

    inline subroutine performSubtraction(r1: int, r2: int, carry: int): byte {
        local result: int = r1 - r2 - carry;

        local Rd7: boolean = r1[7];
        local Rr7: boolean = r2[7];
        local R7:  boolean = result[7];
        local Rd3: boolean = r1[3];
        local Rr3: boolean = r2[3];
        local R3:  boolean = result[3];

        // set the flags as per instruction set documentation.
        H = (!Rd3 and Rr3) or (Rr3 and R3) or (R3 and !Rd3);
        C = (!Rd7 and Rr7) or (Rr7 and R7) or (R7 and !Rd7);
        N = R7;
        Z = low(result) == 0;
        V = (Rd7 and !Rr7 and !R7) or (!Rd7 and Rr7 and R7);
        S = N xor V;

        return low(result);
    };

    // perform subtraction, but preserve zero flag if result is zero
    inline subroutine performSubtractionPZ(r1: int, r2: int, carry: int): byte {
        local result: int = r1 - r2 - carry;

        local Rd7: boolean = r1[7];
        local Rr7: boolean = r2[7];
        local R7:  boolean = result[7];
        local Rd3: boolean = r1[3];
        local Rr3: boolean = r2[3];
        local R3:  boolean = result[3];

        // set the flags as per instruction set documentation.
        H = (!Rd3 and Rr3) or (Rr3 and R3) or (R3 and !Rd3);
        C = (!Rd7 and Rr7) or (Rr7 and R7) or (R7 and !Rd7);
        N = R7;
        Z = (low(result) == 0) and Z;
        V = (Rd7 and !Rr7 and !R7) or (!Rd7 and Rr7 and R7);
        S = N xor V;

        return low(result);
    };

    inline subroutine performLeftShift(r1: int, lowbit: boolean): byte {
        local result: int = r1 << 1;
        result[0] = lowbit;

        H = result[4];
        C = result[8];
        N = result[7];
        Z = low(result) == 0;
        V = N xor C;
        S = N xor V;

        return low(result);
    };

    inline subroutine performRightShift(r1: int, highbit: boolean): byte {
        local result: int = ((r1 & 0xff) >> 1);
        result[7] = highbit;

        C = r1[0];
        N = highbit;
        Z = low(result) == 0;
        V = N xor C;
        S = N xor V;

        return low(result);
    };

    inline subroutine performOr(r1: int, r2: int): byte {
        local result: int = r1 | r2;

        N = result[7];
        Z = low(result) == 0;
        V = false;
        S = N xor V;

        return low(result);
    };

    inline subroutine performAnd(r1: int, r2: int): byte {
        local result: int = r1 & r2;

        N = result[7];
        Z = low(result) == 0;
        V = false;
        S = N xor V;

        return low(result);
    };

    inline subroutine relativeBranch(target: int): void {
        nextPC = relative(target);
        cyclesConsumed = cyclesConsumed + 1;
    };

    inline subroutine relative(target: int): int {
        return (target * 2) + nextPC;
    };

    inline subroutine absolute(target: int): int {
        return target * 2;
    };

    inline subroutine skip(): void {
        local dist: int = $isize(nextPC);
        nextPC = nextPC + dist;
        if ( dist == 4 ) cyclesConsumed = cyclesConsumed + 2;
        else cyclesConsumed = cyclesConsumed + 1;
    };

    inline subroutine pushPC(npc: int): void {
        npc = npc / 2;
        pushByte(low(npc));
        pushByte(high(npc));
    };

    inline subroutine popPC(): int {
        local high: byte = popByte();
        local low: byte = popByte();
        return uword(low, high) * 2;
    };

    inline subroutine get_wreg(r: Register): int {
        return ($regs(r+1) << 8) | ($regs(r)[7:0]);
    };

    inline subroutine get_ureg(r: Register): int {
        return $regs(r)[7:0];
    };

    inline subroutine set_wreg(r: Register, v: int): void {
        $regs(r) = low(v);
        $regs(r+1) = high(v);
    };

    inline subroutine low(v: int): byte {
        return v:byte;
    };

    inline subroutine high(v: int): byte {
        return (v >> 8):byte;
    };

    inline subroutine uword(low: byte, high: byte): int {
        return ((high << 8) | low)[15:0];
    };

    external bit(b: boolean): int;
    external popByte(): byte;
    external pushByte(b: byte): void;


//---------------------------------------------------------------------------
//  BEGIN INSTRUCTION SET DESCRIPTION
//---------------------------------------------------------------------------

    instruction "adc" rd: GPR, rr: GPR {
        encoding = GPRGPR where { opcode = 0b000111, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            $regs(rd) = performAddition($uregs(rd), $uregs(rr), bit(C));
        };
    };

    instruction "add" rd: GPR, rr: GPR { // add second register to first
        encoding = GPRGPR where { opcode = 0b000011, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            $regs(rd) = performAddition($uregs(rd), $uregs(rr), 0);
        };
    };

    instruction "adiw" rd: RDL, imm: IMM6 { // add immediate to word register
        encoding = { 0b10010110, imm[5:4], rd[1:0], imm[3:0] };
        cycles = 2;
        execute = {
            local r1: int       = $wregs(rd);
            local result: int   = r1 + imm;
            local R15: boolean  = result[15];
            local Rdh7: boolean = r1[15];

            C = (!R15 and Rdh7);
            N = (R15);
            V = (!Rdh7 and R15);
            Z = (result[15:0] == 0);
            S = (N xor V);

            $wregs(rd) = result;
        };
    };

    instruction "and" rd: GPR, rr: GPR { // and first register with second
        encoding = GPRGPR where { opcode = 0b001000, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            $regs(rd) = performAnd($regs(rd), $regs(rr));
        };
    };

    instruction "andi" rd: HGPR, imm: IMM8 { // and register with immediate
        encoding = HGPRIMM8 where { opcode = 0b0111, r1 = rd, imm1 = imm };
        cycles = 1;
        execute = {
            $regs(rd) = performAnd($regs(rd), imm);
        };
    };

    instruction "asr" rd: GPR {
        encoding = GPR where { opcode = 0b1001010, r1 = rd };
        cycles = 1;
        execute = {
            local r1: byte = $regs(rd);
            $regs(rd) = performRightShift(r1, r1[7]);
        };
    };

    // BCLR is the general form of clearing a bit in the SREG register
    // each case has its own name: CLI, CLS, CLS, etc
    pseudo instruction "bclr" bit: IMM3 {
        encoding = CLR where { bit = bit };
        cycles = 1;
        execute = {
            $ioregs(SREG)[bit] = false;
        };
    };

    instruction "bld" rr: GPR, bit: IMM3 {
        encoding = { 0b1111100, rr[4:0], 0b0, bit[2:0] };
        cycles = 1;
        execute = {
            $regs(rr)[bit] = T;
        };
    };

    // BRBC is the general form of the "branch if bit is clear" instruction
    // each special case has its own name, and thus the general one is pseudo
    pseudo instruction "brbc" bit: IMM3, target: SREL {
        encoding = BRCLR where { bit = bit };
        cycles = 1;
        execute = {
            if (!($ioregs(SREG)[bit]) ) relativeBranch(target);
        };
    };

    // BRBC is the general form of the "branch if bit is set" instruction
    // each special case has its own name, and thus the general one is pseudo
    pseudo instruction "brbs" bit: IMM3, target: SREL {
        encoding = BRSET where { bit = bit };
        cycles = 1;
        execute = {
            if ( $ioregs(SREG)[bit] ) relativeBranch(target);
        };
    };

    instruction "brcc" target: SREL {
        encoding = BRCLR where { bit = 0b000 };
        cycles = 1;
        execute = {
            if ( !C ) relativeBranch(target);
        };
    };

    // BRCS is equivalen to the BRLO instruction, thus this one is a pseudo instruction
    pseudo instruction "brcs" target: SREL {
        encoding = BRSET where { bit = 0b000 };
        cycles = 1;
        execute = {
            if ( C ) relativeBranch(target);
        };
    };

    instruction "break" {
        encoding = { 0b10010101, 0b10011000 };
        cycles = 1;
        execute = {
            stop();
        };
    };

    instruction "breq" target: SREL {
        encoding = BRSET where { bit = 0b001 };
        cycles = 1;
        execute = {
            if ( Z ) relativeBranch(target);
        };
    };

    instruction "brge" target: SREL{
        encoding = BRCLR where { bit = 0b100 };
        cycles = 1;
        execute = {
            if ( !S ) relativeBranch(target);
        };
    };

    instruction "brhc" target: SREL {
        encoding = BRCLR where { bit = 0b101 };
        cycles = 1;
        execute = {
            if ( !H ) relativeBranch(target);
        };
    };

    instruction "brhs" target: SREL {
        encoding = BRSET where { bit = 0b101 };
        cycles = 1;
        execute = {
            if ( H ) relativeBranch(target);
        };
    };

    instruction "brid" target: SREL {
        encoding = BRCLR where { bit = 0b111 };
        cycles = 1;
        execute = {
            if ( !I ) relativeBranch(target);
        };
    };

    instruction "brie" target: SREL {
        encoding = BRSET where { bit = 0b111 };
        cycles = 1;
        execute = {
            if ( I ) relativeBranch(target);
        };
    };

    instruction "brlo" target: SREL {
        encoding = BRSET where { bit = 0b000 };
        cycles = 1;
        execute = {
            if ( C ) relativeBranch(target);
        };
    };

    instruction "brlt" target: SREL {
        encoding = BRSET where { bit = 0b100 };
        cycles = 1;
        execute = {
            if ( S ) relativeBranch(target);
        };
    };

    instruction "brmi" target: SREL {
        encoding = BRSET where { bit = 0b010 };
        cycles = 1;
        execute = {
            if ( N ) relativeBranch(target);
        };
    };

    instruction "brne" target: SREL {
        encoding = BRCLR where { bit = 0b001 };
        cycles = 1;
        execute = {
            if ( !Z ) relativeBranch(target);
        };
    };

    instruction "brpl" target: SREL {
        encoding = BRCLR where { bit = 0b010 };
        cycles = 1;
        execute = {
            if ( !N ) relativeBranch(target);
        };
    };

    // BRSH is equivalent to the BRCC instruction
    pseudo instruction "brsh" target: SREL {
        encoding = BRCLR where { bit = 0b000 };
        cycles = 1;
        execute = {
            if ( !C ) relativeBranch(target);
        };
    };

    instruction "brtc" target: SREL {
        encoding = BRCLR where { bit = 0b110 };
        cycles = 1;
        execute = {
            if ( !T ) relativeBranch(target);
        };
    };

    instruction "brts" target: SREL {
        encoding = BRSET where { bit = 0b110 };
        cycles = 1;
        execute = {
            if ( T ) relativeBranch(target);
        };
    };

    instruction "brvc" target: SREL {
        encoding = BRCLR where { bit = 0b011 };
        cycles = 1;
        execute = {
            if ( !V ) relativeBranch(target);
        };
    };

    instruction "brvs" target: SREL {
        encoding = BRSET where { bit = 0b011 };
        cycles = 1;
        execute = {
            if ( V ) relativeBranch(target);
        };
    };

    // BRSET is the general form of the "branch if bit is set" instruction
    // each possibility has its own name, so the general case is a pseudo instruction
    pseudo instruction "bset" bit: IMM3 {
        encoding = SET where { bit = bit };
        cycles = 1;
        execute = {
            $ioregs(SREG)[bit] = true;
        };
    };

    instruction "bst" rr: GPR, bit: IMM3 {
        encoding = { 0b1111101, rr[4:0], 0b0, bit[2:0] };
        cycles = 1;
        execute = {
            T = $regs(rr)[bit];
        };
    };

    instruction "call" target: PADDR {
        encoding = ABS4 where { opcode = 0b1001010111 };
        cycles = 4;
        execute = {
            pushPC(nextPC);
            nextPC = absolute(target);
        };
    };

    instruction "cbi" ior: IMM5, bit: IMM3 {
        encoding = { 0b10011000, ior[4:0], bit[2:0] };
        cycles = 2;
        execute = {
            $ioregs(ior)[bit] = false;
        };
    };

    // CBR (clear bits in register) is equivalent to an ANDI instruction
    // and is encoded as an ANDI instruction with the operand complemented
    pseudo instruction "cbr" rd: HGPR, imm: IMM8 {
        encoding = HGPRIMM8 where { opcode = 0b0111, r1 = rd, imm1 = ~imm };
        cycles = 1;
        execute = {
            $regs(rd) = performAnd($regs(rd), ~imm);
        };
    };

    instruction "clc" {
        encoding = CLR where { bit = 0b000 };
        cycles = 1;
        execute = {
            C = false;
        };
    };

    instruction "clh" {
        encoding = CLR where { bit = 0b101 };
        cycles = 1;
        execute = {
            H = false;
        };
    };

    instruction "cli" {
        encoding = CLR where { bit = 0b111 };
        cycles = 1;
        execute = {
            disableInterrupts();
        };
    };

    instruction "cln" {
        encoding = CLR where { bit = 0b010 };
        cycles = 1;
        execute = {
            N = false;
        };
    };

    // CLR (clear register A) is equivalent to EOR A, A (exclusive-or A with itself)
    pseudo instruction "clr" rd: GPR {
        // note that this is equivalent to eor rd, rd
        encoding = GPRGPR where { opcode = 0b001001, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            S = false;
            V = false;
            N = false;
            Z = true;
            $regs(rd) = low(0);
        };
    };

    instruction "cls" {
        encoding = CLR where { bit = 0b100 };
        cycles = 1;
        execute = {
            S = false;
        };
    };

    instruction "clt" {
        encoding = CLR where { bit = 0b110 };
        cycles = 1;
        execute = {
            T = false;
        };
    };

    instruction "clv" {
        encoding = CLR where { bit = 0b011 };
        cycles = 1;
        execute = {
            V = false;
        };
    };

    instruction "clz" {
        encoding = CLR where { bit = 0b001 };
        cycles = 1;
        execute = {
            Z = false;
        };
    };

    instruction "com" rd: GPR {
        encoding = { 0b1001010, rd[4:0], 0b0000 };
        cycles = 1;
        execute = {
            local result: int = 0xff - $regs(rd);

            C = true;
            N = result[7];
            Z = low(result) == 0;
            V = false;
            S = N xor V;

            $regs(rd) = low(result);
        };
    };

    instruction "cp" rd: GPR, rr: GPR {
        encoding = GPRGPR where { opcode = 0b000101, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            // perform subtraction for flag side effects.
            performSubtraction($regs(rd), $regs(rr), 0);
        };
    };

    instruction "cpc" rd: GPR, rr: GPR {
        encoding = GPRGPR where { opcode = 0b000001, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            // perform subtraction for flag side effects.
            performSubtractionPZ($regs(rd), $regs(rr), bit(C));
        };
    };

    instruction "cpi" rd: HGPR, imm: IMM8 {
        encoding = HGPRIMM8 where { opcode = 0b0011, r1 = rd, imm1 = imm };
        cycles = 1;
        execute = {
            // perform subtraction for flag side effects.
            performSubtraction($regs(rd), imm, 0);
        };
    };

    instruction "cpse" rd: GPR, rr: GPR {
        encoding = GPRGPR where { opcode = 0b000100, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            local r1: int = $regs(rd);
            local r2: int = $regs(rr);
            // perform subtraction for flag side effects.
            performSubtraction(r1, r2, 0);
            if (r1 == r2) skip();
        };
    };

    instruction "dec" rd: GPR {
        encoding = { 0b1001010, rd[4:0], 0b1010 };
        cycles = 1;
        execute = {
            local r1: int = $uregs(rd);
            local result: byte = low(r1 - 1);

            N = result[7];
            Z = result == 0;
            V = r1 == 0x80;
            S = N xor V;

            $regs(rd) = result;
        };
    };

    instruction "eicall" {
        encoding = { 0b10010101, 0b00011001 };
        cycles = 4;
        execute = {
            // TODO: implement me
        };
    };

    instruction "eijmp" {
        encoding = { 0b10010100, 0b00011001 };
        cycles = 2;
        execute = {
            // TODO: implement me
        };
    };

    instruction "elpm" {
        encoding = { 0b10010101, 0b11011000 };
        cycles = 3;
        execute = {
            local addr: int = $wregs(RZ);
            addr[23:16] = $ioregs(RAMPZ);
            $regs(R0) = $program(addr);
        };
    };

    instruction "elpmd" rd: GPR, rr: Z {
        encoding = { 0b1001000, rd[4:0], 0b0110 };
        syntax = "elpm %rd, Z";
        cycles = 3;
        execute = {
            local addr: int = $wregs(RZ);
            addr[23:16] = $ioregs(RAMPZ);
            $regs(rd) = $program(addr);
        };
    };

    instruction "elpmpi" rd: GPR, rr: Z {
        encoding = { 0b1001000, rd[4:0], 0b0111 };
        syntax = "elpm %rd, Z+";
        cycles = 3;
        execute = {
            local addr: int = $wregs(RZ);
            addr[23:16] = $ioregs(RAMPZ);
            $regs(rd) = $program(addr);
            $wregs(RZ) = addr + 1;
        };
    };

    instruction "eor" rd: GPR, rr: GPR {
        encoding = GPRGPR where { opcode = 0b001001, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            local result: byte = low($regs(rd) ^ $regs(rr));

            N = result[7];
            Z = result == 0;
            V = false;
            S = N xor V;

            $regs(rd) = result;
        };
    };

    instruction "fmul" rd: MGPR, rr: MGPR {
        encoding = MGPRMGPR where { opcode = 0b0000011, s1 = 0, r1 = rd, s2 = 1, r2 = rr };
        cycles = 2;
        execute = {
            local result: int = ($uregs(rd) * $uregs(rr)) << 1;
            Z = result[15:0] == 0;
            C = result[16];
            $regs(R0) = low(result);
            $regs(R1) = high(result);
        };
    };

    instruction "fmuls" rd: MGPR, rr: MGPR {
        encoding = MGPRMGPR where { opcode = 0b0000011, s1 = 1, r1 = rd, s2 = 0, r2 = rr };
        cycles = 2;
        execute = {
            local result: int = ($regs(rd) * $regs(rr)) << 1;
            Z = result[15:0] == 0;
            C = result[16];
            $regs(R0) = low(result);
            $regs(R1) = high(result);
        };
    };

    instruction "fmulsu" rd: MGPR, rr: MGPR {
        encoding = MGPRMGPR where { opcode = 0b0000011, s1 = 1, r1 = rd, s2 = 1, r2 = rr };
        cycles = 2;
        execute = {
            local result: int = ($regs(rd) * $uregs(rr)) << 1;
            Z = result[15:0] == 0;
            C = result[16];
            $regs(R0) = low(result);
            $regs(R1) = high(result);
        };
    };

    instruction "icall" {
        encoding = { 0b10010101, 0b00001001 };
        cycles = 3;
        execute = {
            pushPC(nextPC);
            nextPC = absolute($wregs(RZ));
        };
    };

    instruction "ijmp" {
        encoding = { 0b10010100, 0b00001001 };
        cycles = 2;
        execute = {
            nextPC = absolute($wregs(RZ));
        };
    };

    instruction "in" rd: GPR, imm: IMM6 {
        encoding = { 0b10110, imm[5:4], rd[4:0], imm[3:0] };
        cycles = 1;
        execute = {
            $regs(rd) = $ioregs(imm);
        };
    };

    instruction "inc" rd: GPR {
        encoding = { 0b1001010, rd[4:0], 0b0011 };
        cycles = 1;
        execute = {
            local r1: int = $uregs(rd);
            local result: byte = low(r1 + 1);

            N = result[7];
            Z = result == 0;
            V = r1 == 0x7f;
            S = N xor V;

            $regs(rd) = result;
        };
    };

    instruction "jmp" target: PADDR {
        encoding = ABS4 where { opcode = 0b1001010110 };
        cycles = 3;
        execute = {
            nextPC = absolute(target);
        };
    };

    instruction "ld" rd: GPR, ar: XYZ {
        encoding = { 0b1001000, rd[4:0], 0b1100 } when ar == X;
        encoding = { 0b1000000, rd[4:0], 0b1000 } when ar == Y;
        encoding = { 0b1000000, rd[4:0], 0b0000 } when ar == Z;
        cycles = 2;
        execute = {
            $regs(rd) = $sram($wregs(ar));
        };
    };

    instruction "ldd" rd: GPR, ar: YZ, imm: IMM6 {
        encoding = priority 1 { 0b10, imm[5], 0b0, imm[4:3], 0b0, rd[4:0], ar[0], imm[2:0] };
        syntax = "ldd %rd, %ar+%imm";
        cycles = 2;
        execute = {
            $regs(rd) = $sram($wregs(ar) + imm);
        };
    };

    instruction "ldi" rd: HGPR, imm: IMM8 {
        encoding = HGPRIMM8 where { opcode = 0b1110, r1 = rd, imm1 = imm };
        cycles = 1;
        execute = {
            $regs(rd) = low(imm);
        };
    };

    instruction "ldpd" rd: GPR, ar:XYZ {
        encoding = { 0b1001000, rd[4:0], 0b1110 } when ar == X;
        encoding = { 0b1001000, rd[4:0], 0b1010 } when ar == Y;
        encoding = { 0b1001000, rd[4:0], 0b0010 } when ar == Z;
        syntax = "ld %rd, -%ar";
        cycles = 2;
        execute = {
            local addr: int = $wregs(RX) - 1;
            $regs(rd) = $sram(addr);
            $wregs(RX) = addr;
        };
    };

    instruction "ldpi" rd: GPR, ar:XYZ {
        encoding = { 0b1001000, rd[4:0], 0b1101 } when ar == X;
        encoding = { 0b1001000, rd[4:0], 0b1001 } when ar == Y;
        encoding = { 0b1001000, rd[4:0], 0b0001 } when ar == Z;
        syntax = "ld %rd, %ar+";
        cycles = 2;
        execute = {
            local addr: int = $wregs(RX);
            $regs(rd) = $sram(addr);
            $wregs(RX) = addr + 1;
        };
    };

    instruction "lds" rd: GPR, addr: DADDR {
        encoding = { 0b1001000, rd[4:0], 0b0000, addr[15:0] };
        cycles = 2;
        execute = {
            $regs(rd) = $sram(addr);
        };
    };

    instruction "lpm" {
        encoding = { 0b10010101, 0b11001000 };
        cycles = 3;
        execute = {
            $regs(R0) = $program($wregs(RZ));
        };
    };

    instruction "lpmd" rd: GPR, z: Z {
        encoding = { 0b1001000, rd[4:0], 0b0100 };
        syntax = "lpm %rd, Z";
        cycles = 3;
        execute = {
            $regs(rd) = $program($wregs(RZ));
        };
    };

    instruction "lpmpi" rd: GPR, z: Z {
        encoding = { 0b1001000, rd[4:0], 0b0101 };
        syntax = "lpm %rd, Z+";
        cycles = 3;
        execute = {
            local addr: int = $wregs(RZ);
            $regs(rd) = $program(addr);
            $wregs(RZ) = addr + 1;
        };
    };

    // LSL (logical shift left by 1) is encoded as an ADD instruction where
    // the register is simply added to itself
    pseudo instruction "lsl" rd: GPR {
        encoding = GPRGPR where { opcode = 0b000011, r1 = rd, r2 = rd };
        cycles = 1;
        execute = {
            $regs(rd) = performLeftShift($regs(rd), false);
        };
    };

    instruction "lsr" rd: GPR {
        encoding = { 0b1001010, rd[4:0], 0b0110 };
        cycles = 1;
        execute = {
            $regs(rd) = performRightShift($regs(rd), false);
        };
    };

    instruction "mov" rd: GPR, rr: GPR {
        encoding = GPRGPR where { opcode = 0b001011, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            $regs(rd) = $regs(rr);
        };
    };

    instruction "movw" rd: EGPR, rr: EGPR {
        encoding = { 0b00000001, rd[3:0], rr[3:0] };
        cycles = 1;
        execute = {
            $wregs(rd) = $wregs(rr);
        };
    };

    instruction "mul" rd: GPR, rr: GPR {
        encoding = GPRGPR where { opcode = 0b100111, r1 = rd, r2 = rr };
        cycles = 2;
        execute = {
            local result: int = $uregs(rd) * $uregs(rr);
            C = result[15];
            Z = (result[15:0] == 0);
            $wregs(R0) = result;
        };
    };

    instruction "muls" rd: HGPR, rr: HGPR {
        encoding = { 0b00000010, rd[3:0], rr[3:0] };
        cycles = 2;
        execute = {
            local result: int = $regs(rd) * $regs(rr);
            C = result[15];
            Z = (result[15:0] == 0);
            $wregs(R0) = result;
        };
    };

    instruction "mulsu" rd: MGPR, rr: MGPR {
        encoding = { 0b00000011, 0b0, rd[2:0], 0b0, rr[2:0] };
        cycles = 2;
        execute = {
            local result: int = $regs(rd) * $uregs(rr);
            C = result[15];
            Z = (result[15:0] == 0);
            $wregs(R0) = result;
        };
    };

    instruction "neg" rd: GPR {
        encoding = { 0b1001010, rd[4:0], 0b0001 };
        cycles = 1;
        execute = {
            $regs(rd) = performSubtraction(0, $regs(rd), 0);
        };
    };

    instruction "nop" {
        encoding = { 0b00000000, 0b00000000 };
        cycles = 1;
        execute = {
            // do nothing.
        };
    };

    instruction "or" rd: GPR, rr: GPR {
        encoding = GPRGPR where { opcode = 0b001010, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            $regs(rd) = performOr($regs(rd), $regs(rr));
        };
    };

    instruction "ori" rd: HGPR, imm: IMM8 {
        encoding = HGPRIMM8 where { opcode = 0b0110, r1 = rd, imm1 = imm };
        cycles = 1;
        execute = {
            $regs(rd) = performOr($regs(rd), imm);
        };
    };

    instruction "out" ior: IMM6, rr: GPR {
        encoding = { 0b10111, ior[5:4], rr[4:0], ior[3:0] };
        cycles = 1;
        execute = {
            $ioregs(ior) = $regs(rr);
        };
    };

    instruction "pop" rd: GPR {
        encoding = { 0b1001000, rd[4:0], 0b1111 };
        cycles = 2;
        execute = {
            $regs(rd) = popByte();
        };
    };

    instruction "push" rr: GPR {
        encoding = { 0b1001001, rr[4:0], 0b1111 };
        cycles = 2;
        execute = {
            pushByte($regs(rr));
        };
    };

    instruction "rcall" target: LREL {
        encoding = { 0b1101, target[11:0] };
        cycles = 3;
        execute = {
            pushPC(nextPC);
            nextPC = relative(target);
        };
    };

    instruction "ret" {
        encoding = { 0b10010101, 0b00001000 };
        cycles = 4;
        execute = {
            nextPC = popPC();
        };
    };

    instruction "reti" {
        encoding = { 0b10010101, 0b00011000 };
        cycles = 4;
        execute = {
            nextPC = popPC();
            enableInterrupts();
            // lastRETI = totalCycles;
            justReturnedFromInterrupt = true;
        };
    };

    instruction "rjmp" target: LREL {
        encoding = { 0b1100, target[11:0] };
        cycles = 2;
        execute = {
            nextPC = relative(target);
        };
    };

    // ROL (rotate left by 1) is encoded an ADC (add with carry instruction)
    // where the register is simply added to itself
    pseudo instruction "rol" rd: GPR {
        encoding = GPRGPR where { opcode = 0b000111, r1 = rd, r2 = rd };
        cycles = 1;
        execute = {
            $regs(rd) = performLeftShift($uregs(rd), C);
        };
    };

    instruction "ror" rd: GPR {
        encoding = { 0b1001010, rd[4:0], 0b0111 };
        cycles = 1;
        execute = {
            $regs(rd) = performRightShift($regs(rd), C);
        };
    };

    instruction "sbc" rd: GPR, rr: GPR {
        encoding = GPRGPR where { opcode = 0b0000010, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            $regs(rd) = performSubtractionPZ($regs(rd), $regs(rr), bit(C));
        };
    };

    instruction "sbci" rd: HGPR, imm: IMM8 {
        encoding = HGPRIMM8 where { opcode = 0b0100, r1 = rd, imm1 = imm };
        cycles = 1;
        execute = {
            $regs(rd) = performSubtractionPZ($regs(rd), imm, bit(C));
        };
    };

    instruction "sbi" ior: IMM5, bit: IMM3 {
        encoding = { 0b10011010, ior[4:0], bit[2:0] };
        cycles = 2;
        execute = {
            $ioregs(ior)[bit] = true;
        };
    };

    instruction "sbic" ior: IMM5, bit: IMM3 {
        encoding = { 0b10011001, ior[4:0], bit[2:0] };
        cycles = 1;
        execute = {
            if (!($ioregs(ior)[bit])) skip();
        };
    };

    instruction "sbis" ior: IMM5, bit: IMM3 {
        encoding = { 0b10011011, ior[4:0], bit[2:0] };
        cycles = 1;
        execute = {
            if ( $ioregs(ior)[bit] ) skip();
        };
    };

    instruction "sbiw" rd: RDL, imm: IMM6 {
        encoding = { 0b10010111, imm[5:4], rd[1:0], imm[3:0] };
        cycles = 2;
        execute = {
            local val: int = $wregs(rd);
            local result: int = val - imm;

            local Rdh7: boolean = val[15];
            local R15: boolean = result[15];

            V = Rdh7 and !R15;
            N = R15;
            Z = result[15:0] == 0;
            C = R15 and !Rdh7;
            S = N xor V;

            $wregs(rd) = result;
        };
    };

    // SBR (set bits in register) is equivalent to the ORI (or with immediate) instruction
    pseudo instruction "sbr" rd: HGPR, imm: IMM8 {
        encoding = HGPRIMM8 where { opcode = 0b0110 };
        cycles = 1;
        execute = {
            $regs(rd) = performOr($regs(rd), imm);
        };
    };

    instruction "sbrc" rr: GPR, bit: IMM3 {
        encoding = { 0b1111110, rr[4:0], 0b0, bit[2:0] };
        cycles = 1;
        execute = {
            if (!($regs(rr)[bit])) skip();
        };
    };

    instruction "sbrs" rr: GPR, bit: IMM3 {
        encoding = { 0b1111111, rr[4:0], 0b0, bit[2:0] };
        cycles = 1;
        execute = {
            if ( $regs(rr)[bit] ) skip();
        };
    };

    instruction "sec" {
        encoding = SET where { bit = 0b000 };
        cycles = 1;
        execute = {
            C = true;
        };
    };

    instruction "seh" {
        encoding = SET where { bit = 0b101 };
        cycles = 1;
        execute = {
            H = true;
        };
    };

    instruction "sei" {
        encoding = SET where { bit = 0b111 };
        cycles = 1;
        execute = {
            enableInterrupts();
        };
    };

    instruction "sen" {
        encoding = SET where { bit = 0b010 };
        cycles = 1;
        execute = {
            N = true;
        };
    };

    pseudo instruction "ser" rd: HGPR {
        encoding = { 0b11101111, rd[4:1], 0b1111 };
        cycles = 1;
        execute = {
            $regs(rd) = low(0xff);
        };
    };

    instruction "ses" {
        encoding = SET where { bit = 0b100 };
        cycles = 1;
        execute = {
            S = true;
        };
    };

    instruction "set" {
        encoding = SET where { bit = 0b110 };
        cycles = 1;
        execute = {
            T = true;
        };
    };

    instruction "sev" {
        encoding = SET where { bit = 0b011 };
        cycles = 1;
        execute = {
            V = true;
        };
    };

    instruction "sez" {
        encoding = SET where { bit = 0b001 };
        cycles = 1;
        execute = {
            Z = true;
        };
    };

    instruction "sleep" {
        encoding = { 0b10010101, 0b10001000 };
        cycles = 1;
        execute = {
            enterSleepMode();
        };
    };

    instruction "spm" {
        encoding = { 0b10010101, 0b11101000 };
        cycles = 1;
        execute = {
            // TODO: don't know how this instruction behaves
        };
    };

    instruction "st" ar: XYZ, rr: GPR {
        encoding = { 0b1001001, rr[4:0], 0b1100 } when ar == X;
        encoding = { 0b1000001, rr[4:0], 0b1000 } when ar == Y;
        encoding = { 0b1000001, rr[4:0], 0b0000 } when ar == Z;
        cycles = 2;
        execute = {
            $sram($wregs(ar)) = $regs(rr);
        };
    };

    instruction "std" ar: YZ, imm: IMM6, rr: GPR {
        encoding = priority 1 { 0b10, imm[5], 0b0, imm[4:3], 0b1, rr[4:0], ar[0], imm[2:0] };
        syntax = "std %ar+%imm, %rr";
        cycles = 2;
        execute = {
            $sram($wregs(ar) + imm) = $regs(rr);
        };
    };

    instruction "stpd" ar: XYZ, rr: GPR {
        encoding = { 0b1001001, rr[4:0], 0b1110 } when ar == X;
        encoding = { 0b1001001, rr[4:0], 0b1010 } when ar == Y;
        encoding = { 0b1001001, rr[4:0], 0b0010 } when ar == Z;
        syntax = "st -%ar, %rr";
        cycles = 2;
        execute = {
            local addr: int = $wregs(ar) - 1;
            $sram(addr) = $regs(rr);
            $wregs(ar) = addr;
        };
    };

    instruction "stpi" ar: XYZ, rr: GPR {
        encoding = { 0b1001001, rr[4:0], 0b1101 } when ar == X;
        encoding = { 0b1001001, rr[4:0], 0b1001 } when ar == Y;
        encoding = { 0b1001001, rr[4:0], 0b0001 } when ar == Z;
        syntax = "st %ar+, %rr";
        cycles = 2;
        execute = {
            local addr: int = $wregs(ar);
            $sram(addr) = $regs(rr);
            $wregs(ar) = addr + 1;
        };
    };

    instruction "sts" addr: DADDR, rr: GPR {
        encoding = { 0b1001001, rr[4:0], 0b0000, addr[15:0] };
        cycles = 2;
        execute = {
            $sram(addr) = $regs(rr);
        };
    };

    instruction "sub" rd: GPR, rr: GPR {
        encoding = GPRGPR where { opcode = 0b000110, r1 = rd, r2 = rr };
        cycles = 1;
        execute = {
            $regs(rd) = performSubtraction($regs(rd), $regs(rr), 0);
        };
    };

    instruction "subi" rd: HGPR, imm: IMM8 {
        encoding = HGPRIMM8 where { opcode = 0b0101, r1 = rd, imm1 = imm };
        cycles = 1;
        execute = {
            $regs(rd) = performSubtraction($regs(rd), imm, 0);
        };
    };

    instruction "swap" rd: GPR {
        encoding = { 0b1001010, rd[4:0], 0b0010 };
        cycles = 1;
        execute = {
            local val: int = $uregs(rd);
            local result: int = 0;
            result[3:0] = val[7:4];
            result[7:4] = val[3:0];
            $regs(rd) = low(result);
        };
    };

    // TST (test) is encoded as an AND instruction
    pseudo instruction "tst" rd: GPR {
        encoding = GPRGPR where { opcode = 0b001000, r1 = rd, r2 = rd };
        cycles = 1;
        execute = {
            local r1: int = $regs(rd);
            V = false;
            Z = low(r1) == 0;
            N = r1[7];
            S = N xor V;
        };
    };

    instruction "wdr" {
        encoding = { 0b10010101, 0b10101000 };
        cycles = 1;
        execute = {
            // do nothing.
        };
    };

}